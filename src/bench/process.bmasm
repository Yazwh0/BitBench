library BitBench.Process.Manager;

import Init="initialisation.bmasm";
 
private static void Setup()
{
/*
//  Process needs:
//  byte: Primary Ram Bank
//  byte: unused
//  byte: Location of stack
//  byte: Location of ZP data + registers
*/
    .segment bench_process, scope=bench_process
    
    .process_data:
    for(var i = 0; i < 256/4; i++)
    {
        .padvar byte primary_bank_@(i)
        .padvar byte state_@(i)
        .padvar byte stack_block_@(i)
        .padvar byte data_block_@(i)
    }

    .endsegment
}

private static void Proc()
{
    .scope bench_process

    .const PrimaryBank = 0
    .const State = 1           ; whether the process has been interrupted, or handed back control
    .const StackBlock = 2
    .const DataBlock = 3

    .proc initialise

        ldx #0
        .loop:
        stz process_data + PrimaryBank, x
        stz process_data + State, x
        stz process_data + StackBlock, x
        stz process_data + DataBlock, x
        inx
        inx
        inx
        inx
        bne loop

        // mark first process as in use so we can't allocate 0
        lda #$ff
        sta process_data + PrimaryBank

        rts

    .endproc

    .segment bench_general, scope=bench_process

    .padvar byte offset
    .padvar byte new_process

    .endsegment

    .scope bench_process

    // Create a process, allocate memory and setup stack
    // Input
    // A: Main bank
    // Output
    // A: Process ID
    // X: Stack Block
    // Y: Data Block
    .proc createprocess

        pha
 
        ; search for first available process ID
        ldx #0
        .loop:
        lda process_data + PrimaryBank, x
        beq process_found                       ; find first process that isn't assigned a bank
        inx
        inx
        inx
        inx
        jmp -loop

        .process_found:
        txa
        clc
        ror
        ror                                     ; A now contains the process ID, X the offset

        sta new_process                         ; store so we can resuse these
        stx offset
        pla
        sta process_data + PrimaryBank, x       ; mark process id as reserved

        ; allocate stack block, A is number of blocks, X is process ID
        ldx new_process
        lda #1
        jsr bench_memory:allocate_block
        bcs error
        pha                                     ; stack block, to be returned in X

        ldx offset                              ; store stack block number in the process struct
        sta process_data + StackBlock, x

        ; allocation data block
        ldx new_process
        lda #1
        jsr bench_memory:allocate_block
        bcs error_pl

        tay                                     ; data block, returned value in Y

        ldx offset
        sta process_data + DataBlock, x

        ; return process ID
        lda new_process
        plx                                     ; set stack block in return register
        rts

        .error_pl:
        phx
        .error:                                 ; out of memory
        sec
        rts

    .endproc

    .scope bench_process

    // Input
    // A: Process ID
    .proc closeprocess

        tax
        stz process_data + PrimaryBank, x
        stz process_data + State, x
        stz process_data + StackBlock, x
        stz process_data + DataBlock, x

        rts

    .endproc


    .segment bench_general, scope=bench_process

    .padvar byte new_bank
    .padvar byte new_process

    .endsegment

    .scope bench_process

    // load a new process, but not activate it. This should be done via the scheduler
    // Input:
    // X,Y : Address of filename
    // A : length of filename
    // Output:
    // A: Process ID
    .proc load

        .const SETNAM = $FFBD
        .const SETLFS = $FFBA
        .const LOAD = $FFD5

        sta bench_zp:scratch
        lda ROM_BANK
        pha
        stz ROM_BANK                        ; switch to kernel as we use it to load

        lda bench_zp:scratch

        ; parameters are in the form for SETNAM
        jsr SETNAM

        lda #1
        ldx #8
        ldy #2
        jsr SETLFS

        ; create process. Need allocation the process to a bank before we can load.
        ; first find the bank where we will load to
        lda #1
        ldx #$fe                            ; temporary process ID
        jsr bench_memory:allocate_bank
        bcs error

        sta new_bank                        ; bank number in A, store for later
        
        jsr createprocess                   ; reserves up blocks and sets up defaults, returns process ID, stack block (x) and data block (sy)
        bcc no_error

    .error:                                 ; out of memory
        pla
        pla
        sec
        rts
 
    .no_error:
        stx bench_general:zp_pointer + 1    ; store stack block number for indirect addressing, for stach initialisation
        stz bench_general:zp_pointer

        sty bench_general:zp_pointer2 + 1   ; stock data block number 
        ;stz bench_general:zp_pointer2      ; not needed, as overriden later

        sta new_process                     ; store process ID to return it
        pha
        ldx new_bank

        jsr bench_memory:update_bank     ; update the bank to the actual process ID

        lda new_bank
        sta RAM_BANK

        lda #0
        ldx #<$a000
        ldy #>$a000
        jsr LOAD 
        bcs error_pl

        pla
        pha
        sta bench_general:application_processid

        ; setup stack, (bitbench_zp:zp_pointer) is a pointer to the stack block
        ; intial stack should be entryaddress, CPU flags, A, X, Y, rom_bank, ram_bank, stack_pointer
        ; CPU flags, A, X and Y are zero so they can be predicted
        ; Rom bank is set to the kernel
        ldy #$ff
        ; Stack pointer
        lda #$f6
        sta (bench_general:zp_pointer), y      ; should be $xxff
        dey

        ; entry address
        lda bench_general:application_entry_h
        sta (bench_general:zp_pointer), y
        dey

        lda bench_general:application_entry_l
        sta (bench_general:zp_pointer), y
        dey

        ; CPU Flags
        lda #0
        sta (bench_general:zp_pointer), y
        dey
        ; A
        sta (bench_general:zp_pointer), y
        dey
        ; X
        sta (bench_general:zp_pointer), y
        dey
        ; Y
        sta (bench_general:zp_pointer), y
        dey
        ; RAM Bank
        lda RAM_BANK
        sta (bench_general:zp_pointer), y
        dey
        ; ROM Bank
        lda #0
        sta (bench_general:zp_pointer), y
        dey

        ; clear data area
        ldy bench_general:application_zp_count
        beq data_copy_done
        lda #$22                                
        sta bench_general:zp_pointer2
        lda #0
    .loop:
        dey
        sta (bench_general:zp_pointer2), y
        bne -loop
    .data_copy_done:

        ; get process priority value
        ldy bench_general:application_frame_update

        lda #1
        sta RAM_BANK

        lda new_process
        jsr bench_scheduler:enable_process ; set the process to run and the required frames
        
        pla
        sta RAM_BANK                        ; restore original ROM Bank

        pla
        sta ROM_BANK

        lda new_process
        clc
        rts

        .error_pl:
        pla
        pla
    .endproc

    ; returns the primary bank for the process in A
    ; inputs:
    ; A: ProcessId
    ; returns:
    ; A: BankId
    .proc get_bank

        clc
        rol ; * 4
        rol
        tax

        lda process_data + PrimaryBank, x

        rts

    .endproc

    ; called by the current process to hand back control    
    .proc process_sleep
        sei     ; we dont want to interrupt this process
        ; add CPU flags, and AYX so it looks like a interrupt
        sta bench_general:zp_pointer
        
        lda #0      ; We dont guanaratee flags coming out of process_sleep
        pha         ; CPU Flags

        lda bench_general:zp_pointer
        pha         ; A
        phx         ; X
        phy         ; Y

        ; always store these for the switcher
        lda RAM_BANK
        pha
        lda ROM_BANK
        pha

        lda #bench_general:bank_bench  ; Bench uses bank 1.
        sta RAM_BANK 

        jsr bench_scheduler:get_next_process ; returns 0 if nothing to do

        beq nothing_to_do        

        jsr bench_switcher:activate_process_interrupt 

        ; stack here will always have these values even if switched
        pla 
        sta ROM_BANK
        pla
        sta RAM_BANK

        stz DC_BORDER

        plx
        ply
        pla

        rti

    .nothing_to_do:    
        jsr bench_switcher:store_process

        lda #$ff
        sta bench_switcher:current_process

        ; ldx #$fe
        ; txs

        lda #>idle_loop
        pha
        lda #<idle_loop
        pha
        lda #0
        pha     ; cpu flags

        stz DC_BORDER

        rti        
    .endproc

    .proc idle_loop

    .loop:
        cli
        wai
        jmp loop

    .endproc

    .endscope
}

public override void Initialise()
{
    Init.Add(Proc);
    Setup();
}
